IF
Global("Chapter","GLOBAL",14)
AreaCheck("ar0311")
Global("Saf2_StartAmn","Global",0)
See(Player1)
See("Gaelan")
THEN
RESPONSE #100
SetGlobal("Saf2_StartAmn","Global",1)
SetGlobal("O#CoranMovesDestroys","GLOBAL",2)
StartDialogNoSet(Player1)
END

IF
Global("bd_SafanaRomance1","GLOBAL",0)
Gender(Player1,MALE)
InParty("Safana")
AreaType(OUTDOOR)
!AreaType(CITY)
THEN
RESPONSE #100
SetGlobal("bd_SafanaRomance1","GLOBAL",1)
SetGlobal("bd_safana_romanceactive","global",1)
END

IF
Global("bd_SafanaRomance1","GLOBAL",2)
Global("bd_SafanaRomance2","GLOBAL",0)
InParty("Safana")
OR(11)
AreaCheck("ar0314")
AreaCheck("ar0406")
AreaCheck("ar0509")
AreaCheck("ar0511")
AreaCheck("ar0513")
AreaCheck("ar0704")
AreaCheck("ar0709")
AreaCheck("ar1105")
AreaCheck("ar1602")
AreaCheck("ar2010")
AreaCheck("ar2203")
THEN
RESPONSE #100
SetGlobal("bd_SafanaRomance2","GLOBAL",1)
END

IF
Global("bd_SafanaRomance2","GLOBAL",2)
!Global("bd_safana_romanceactive","global",3)
Global("bd_SafanaRomance3","GLOBAL",0)
InParty("Safana")
AreaType(OUTDOOR)
OR(7)
PartyHasItem("amul13")
PartyHasItem("ring16")
PartyHasItem("misc37")
PartyHasItem("misc38")
PartyHasItem("misc39")
PartyHasItem("misc40")
PartyHasItem("ring18")
THEN
RESPONSE #100
SetGlobal("bd_SafanaRomance3","GLOBAL",1)
END

IF
Global("bd_SafanaRomance3","GLOBAL",2)
!Global("bd_safana_romanceactive","global",3)
Global("bd_SafanaRomance4","GLOBAL",0)
InParty("Safana")
!AreaType(OUTDOOR)
PartyRested()
THEN
RESPONSE #100
SetGlobal("bd_SafanaRomance4","GLOBAL",1)
END

IF
!Global("bd_safana_romanceactive","global",3)
Global("bd_SafanaRomance9","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("bd_SafanaRomance9","GLOBAL",0)
END

IF
Global("bd_SafanaRomance9","GLOBAL",3)
!Global("bd_safana_romanceactive","global",3)
THEN
RESPONSE #100
SetGlobal("bd_safana_romanceactive","global",3)
END

IF
GlobalLT("bd_SafanaRomance6","GLOBAL",1)
!Global("bd_safana_romanceactive","global",3)
AreaCheck("ar2900")
InParty("Safana")
THEN
RESPONSE #100
SetGlobal("bd_SafanaRomance6","GLOBAL",1)
END

IF
OR(19)
Global("bd_SafanaRomance1","GLOBAL",1)
Global("bd_SafanaRomance2","GLOBAL",1)
Global("bd_SafanaRomance3","GLOBAL",1)
Global("bd_SafanaRomance4","GLOBAL",1)
Global("bd_SafanaRomance5","GLOBAL",1)
Global("bd_SafanaRomance6","GLOBAL",1)
Global("bd_SafanaAmn1","GLOBAL",1)
Global("bd_SafanaAmn2","GLOBAL",2)
Global("bd_SafanaAmnR","GLOBAL",2)
Global("bd_SafanaAmnR","GLOBAL",3)
Global("BD_safsancity","Locals",1)
Global("BD_SafNali1","LOCALS",1)
Global("BD_SafSewers","LOCALS",1)
Global("BD_SafVali1","LOCALS",1)
Global("BD_TrollComment","LOCALS",1)
Global("BD_WhereisComment","LOCALS",1)
Global("SafanEggChange","Global",1)
Global("BD_Safadesh","Global",2)
Global("BD_Safadesh","Global",4)
InParty("Safana")
See(Player1)
!ActuallyInCombat()
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END

IF
Global("bd_SafanaAmn1","GLOBAL",0)
Global("TalkedToRenal","GLOBAL",0)
AreaType(OUTDOOR)
THEN
RESPONSE #100
SetGlobal("bd_SafanaAmn1","GLOBAL",1)
END

IF
Global("bd_SafanaAmn2","GLOBAL",0)
See("MaeVar")
THEN
RESPONSE #100
SetGlobal("bd_SafanaAmn2","GLOBAL",1)
END

IF
Global("bd_SafanaAmn2","GLOBAL",1)
!AreaCheck("ar0301")
THEN
RESPONSE #100
SetGlobal("bd_SafanaAmn2","GLOBAL",2)
END

IF
Global("bd_SafanaAmnR","GLOBAL",1)
Global("RenalJob","GLOBAL",1)
AreaType(OUTDOOR)
THEN
RESPONSE #100
SetGlobal("bd_SafanaAmnR","GLOBAL",2)
END

IF
Global("bd_SafanaAmnR","GLOBAL",1)
!Global("RenalJob","GLOBAL",1)
AreaType(OUTDOOR)
THEN
RESPONSE #100
SetGlobal("bd_SafanaAmnR","GLOBAL",3)
END

IF
Global("BD_SafanKidnap","ar2600",4)
!Dead("C6Lanf")
Global("Saf_inBattle","ar2600",0)
THEN
RESPONSE #100
SetGlobal("Saf_inBattle","ar2600",1)
MoveToPoint([3197.747])
END

IF
Global("BD_SafanKidnap","ar2600",5)  
!InParty(Myself)
THEN
RESPONSE #100
AttackReevaluate([ENEMY],30)
END

IF
AreaCheck("AR1302")
!Dead("TorGal")
!InParty("tashia")
Global("BD_TrollComment","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("BD_TrollComment","LOCALS",1)
END

IF
Global("BD_SafNali1","LOCALS",0)
InParty("Nalia")
InParty(Myself)
AreaType(DUNGEON)
!ActuallyInCombat()
Delay(6)
THEN
RESPONSE #100
SetGlobal("BD_SafNali1","LOCALS",1)
END

IF
Global("BD_SafSewers","LOCALS",0)
AreaCheck("ar0701")
GlobalGT("BeholderPlot","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BD_SafSewers","LOCALS",1)
END

IF
Global("BD_SafVali1","LOCALS",0)
InParty("Valygar")
InParty(Myself)
AreaType(CITY)
!ActuallyInCombat()
Delay(12)
THEN
RESPONSE #100
SetGlobal("BD_SafVali1","LOCALS",1)
END

IF
Global("BD_safsancity","Locals",0)
InParty("CVSandr")
InParty(Myself)
OR(3)
AreaCheck("ar0500")
AreaCheck("ar0700")
AreaCheck("ar0300")
THEN
RESPONSE #100
SetGlobal("BD_safsancity","Locals",1)
END

IF
Global("BD_WhereisComment","LOCALS",0)
GlobalLT("Chapter","Global",16)
AreaCheck("ar0406")
OR(4)
See("Minsc")
See("Anomen")
See("Jaheira")
See("Firkra01")
THEN
RESPONSE #100
SetGlobal("BD_WhereisComment","LOCALS",1)
END

IF
AreaCheck("ar2201")
!InParty("ppsime")
Contains("misc9t","Eggs")
OR(3)
PartyHasItem("misc9u")
PartyHasItem("misc9v")
PartyHasItem("lk#xeggs")
PartyHasItem("key25")
Global("SafanEggChange","Global",0)
THEN
RESPONSE #100
SetGlobal("SafanEggChange","Global",1)
END

IF
Global("BD_Safaerie","LOCALS",0)
InParty("Aerie")
InParty(Myself)
!AreaType(CITY)
AreaType(OUTDOOR)
Delay(6)
THEN
RESPONSE #100
SetGlobal("BD_Safaerie","LOCALS",1)
ActionOverride("Aerie",Interact("Safana"))
END

IF
Global("BD_Safadesh","Global",1)
AreaCheck("ar1600")
See("ppdoor")
THEN
RESPONSE #100
SetGlobal("BD_Safadesh","Global",2)
END

IF
Global("BD_Safadesh","Global",5)
PartyHasItem("misc8v")
THEN
RESPONSE #100
SetGlobal("BD_Safadesh","Global",6)
AddJournalEntry(@1010,QUEST_DONE)
END

IF
AreaCheck("ar0406")
See("Hendak")
Global("SafHendAff","ar0406",0)
OR(2)
Global("HendakReleased","AR0406",3)
Global("HendakReleased","AR0406",4)
THEN
RESPONSE #100
SetGlobal("SafHendAff","ar0406",1)
END

IF
AreaCheck("ar0406")
Global("SafHendAff","ar0406",2)
RealGlobalTimerExpired("SafHendAffT","ar0406")
THEN
RESPONSE #100
SetGlobal("SafHendAff","ar0406",3)
END

IF
Global("bd_resetconflict","locals",0)
Gender(Player1,MALE)
InParty("Safana")
Global("bd_safana_romanceactive","global",3)
THEN
RESPONSE #100
SetGlobal("bd_resetconflict","locals",1)
SetGlobal("bd_safana_romanceactive","global",1)
END